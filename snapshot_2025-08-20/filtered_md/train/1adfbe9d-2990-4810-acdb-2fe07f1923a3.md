# Threat Report: 2024-07-27: Impact of FrostyGoop ICS Malware on Connected OT Systems


## Key Intelligence
* Date: 2024-07-27
* Threat Level: 1 (High)
* Tags: misp-galaxy:target-information="Ukraine", misp-galaxy:sector="Energy", misp-galaxy:mitre-ics-techniques="Standard Application Layer Protocol", misp-galaxy:mitre-ics-techniques="Unauthorized Command Message", misp-galaxy:mitre-attack-pattern="Create or Modify System Process - T1543", misp-galaxy:mitre-attack-pattern="System Owner/User Discovery - T1033", misp-galaxy:mitre-attack-pattern="System Service Discovery - T1007", misp-galaxy:mitre-attack-pattern="Query Registry - T1012", misp-galaxy:mitre-attack-pattern="Modify Registry - T1112", tlp:white

---

## Indicators of Compromise (IOCs)
### External analysis
* link: https://hub.dragos.com/hubfs/Reports/Dragos-FrostyGoop-ICS-Malware-Intel-Brief-0724_.pdf
* link: https://www.nozominetworks.com/blog/protecting-against-frostygoop-bustleberm-malware
* link: https://github.com/rolfl/modbus — FrostyGoop binaries use a Go Modbus library retrieved from a publicly available Github repository

### Payload delivery
* filename: task_test.json — configuration file

### Payload installation
* yara: // Created by Nozomi Networks Labs

rule Mal_Hacktool_Win64_Bustleberm

{

meta:

   name = "BUSTLEBERM ICS Hacktool"

   author = "Nozomi Networks Labs"

   description = "Detects the BUSTLEBERM ICS Hacktool (also known as FrostyGoop)"

   date = "2024-07-24"

   tlp = "clear"

   x_threat_name = "BUSTLEBERM"

   x_mitre_technique = "T1007, T1012, T1033, T1112, T1543, T0869, T0855"

   reference = "https://hub.dragos.com/hubfs/Reports/Dragos-FrostyGoop-ICS-Malware-Intel-Brief-0724_.pdf"

   hash1 = "5d2e4fd08f81e3b2eb2f3eaae16eb32ae02e760afc36fa17f4649322f6da53fb"

   hash2 = "a63ba88ad869085f1625729708ba65e87f5b37d7be9153b3db1a1b0e3fed309c"

strings:

   $go = "Go build ID:" ascii fullword

   $modbus_1 = "github.com/rolfl/modbus" ascii fullword

   $modbus_2 = "\x00main.MbConfig.writeMultiple\x00" ascii

   $rtn_1 = "\x00main.TaskList.executeCommand\x00" ascii

   $rtn_2 = "\x00main.TaskList.getTaskIpList\x00" ascii

   $rtn_3 = "\x00main.TaskList.getIpList\x00" ascii

   $rtn_4 = "\x00main.TargetList.getTargetIpList\x00" ascii

condition:

   uint16(0) == 0x5a4d and

   filesize <= 10MB and

   $go and

   any of ($modbus_*) and

   2 of ($rtn_*)

}
* yara: rule MAL_Go_Modbus_Jul24_1 {
   meta:
      description = "Detects characteristics reported by Dragos for FrostyGoop ICS malware"
      author = "Florian Roth"
      reference = "https://hub.dragos.com/hubfs/Reports/Dragos-FrostyGoop-ICS-Malware-Intel-Brief-0724_.pdf"
      date = "2024-07-23"
      modified = "2024-07-24"
      score = 75
      hash1 = "5d2e4fd08f81e3b2eb2f3eaae16eb32ae02e760afc36fa17f4649322f6da53fb"
   strings:
      $a1 = "Go build"

      $sa1 = "github.com/rolfl/modbus"

      $sb1 = "main.TaskList.executeCommand"
      $sb2 = "main.TargetList.getTargetIpList"
      $sb3 = "main.TaskList.getTaskIpList"
      $sb4 = "main.CycleInfo" fullword
   condition:
      filesize < 30MB
      and (
         $sa1
         and 3 of ($sb*)
      )
      or 4 of them
} — https://raw.githubusercontent.com/Neo23x0/signature-base/master/yara/mal_go_modbus.yar

## Objects
### file — File object describing a file with meta-information
* [Payload delivery] sha256: <sha256>
* [Payload delivery] md5: <md5>
* [Payload delivery] sha1: <sha1>
* [Payload delivery] filename: mb.exe

### file — File object describing a file with meta-information
* [Payload delivery] sha256: <sha256>
* [Payload delivery] md5: <md5>
* [Payload delivery] sha1: <sha1>
